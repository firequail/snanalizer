<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:ravis="org.un.cava.birdeye.ravis.graphLayout.visual.*"
	xmlns:lc="org.un.cava.birdeye.ravis.components.ui.controls.layouterControls.*"
	xmlns:dp="org.un.cava.birdeye.ravis.components.ui.dataPresentation.*"
	xmlns:grafo="sna.analisis.*"
	width="100%" height="100%">

	<mx:Metadata>
		[Event(name=NodeSelectedEvent.NODE_SELECTED, type="sna.analisis.ravis.NodeSelectedEvent")]
	</mx:Metadata>
	
	<mx:RemoteObject id="redesService" destination="redesService">
		<mx:method name="getGrafo" result="crearGrafos(event)"/>
	</mx:RemoteObject>
	
	<mx:Grid id="grid" width="100%" height="100%">
		
	</mx:Grid>
	
	<mx:Script>
		<![CDATA[
			import sna.analisis.ravis.NodeSelectedEvent;
			import mx.containers.GridRow;
			import mx.containers.GridItem;
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.controls.Alert;
			import mx.rpc.events.ResultEvent;
			
			public var puntoDeVistaId:Object;
			
			private var columnaActual:int;
			
			private var rowActual:GridRow;
			
			private function crearGrafos(event:ResultEvent):void {
				var xmlData:XMLList = new XMLList(redesService.getGrafo.lastResult)
				
				grid.removeAllChildren();
				columnaActual = 0;
				
				for each(var xmlGraph:XML in xmlData) {
					var grafo:GrafoRavis = new GrafoRavis();
					grafo.xmlData = xmlGraph;
					grafo.addEventListener(FlexEvent.CREATION_COMPLETE, initGrafo);
					grafo.addEventListener(NodeSelectedEvent.NODE_SELECTED, nodeSelected);
					addGrafo(grafo);
				}
			}
			
			private function addGrafo(grafo:GrafoRavis):void {
				if (columnaActual == 0) {
					rowActual = new GridRow();
					rowActual.percentHeight = 100;
					rowActual.percentWidth = 100;
					grid.addChild(rowActual);
				}
				var item:GridItem = new GridItem();
				item.addChild(grafo);
				item.percentHeight = 100;
				item.percentWidth = 100;
				rowActual.addChild(item);
				
				columnaActual++;
				if (columnaActual > 1){
					columnaActual = 0;
				}
			}
			
			public function refresh():void {
				redesService.getGrafo(puntoDeVistaId as int);
			}
			
			private function initGrafo(event:FlexEvent):void {
				(event.target as GrafoRavis).init();
			}
			
			private function nodeSelected(e:NodeSelectedEvent):void {
				dispatchEvent(e);
			}
			
		]]>
	</mx:Script>
		
</mx:Canvas>
